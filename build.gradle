import io.gitlab.arturbosch.detekt.Detekt
import io.gitlab.arturbosch.detekt.DetektCreateBaselineTask

// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext {
        release = [
                versionName: "4.4.2",
                versionCode: 1
        ]
    }
    ext.kotlin_version = '1.3.61'
    repositories {
        mavenCentral()
        google()
        maven { url 'https://jitpack.io' }
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:4.2.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.google.gms:google-services:4.3.14'
    }
}

plugins {
    id "io.gitlab.arturbosch.detekt" version "1.18.0"
    id("io.github.gradle-nexus.publish-plugin") version "1.1.0"
}

dependencies {
    detektPlugins "io.gitlab.arturbosch.detekt:detekt-formatting:1.18.0"
}

allprojects {
    repositories {
        mavenCentral()
        google()
        maven { url 'https://jitpack.io' }
        jcenter()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

def projectSource = file(projectDir)
def configFile = files("$rootDir/config/detekt/detekt.yml")
def baselineFile = file("$rootDir/config/detekt/baseline.xml")
def kotlinFiles = "**/*.kt"
def resourceFiles = "**/resources/**"
def buildFiles = "**/build/**"
def testFiles = "**/test/**"
def appFiles = "**/app/**"

tasks.register("detektAll", Detekt) {
    description = "Detekt build for all modules"
    parallel = true
    ignoreFailures = false
    autoCorrect = true
    buildUponDefaultConfig = true
    setSource(projectSource)
    baseline.set(baselineFile)
    config.setFrom(configFile)
    include(kotlinFiles)
    exclude(buildFiles)
    exclude(testFiles)
    exclude(appFiles)
    reports {
        html.enabled = true
        xml.enabled = false
        txt.enabled = false
    }
}

tasks.register("detektGenerateBaseline", DetektCreateBaselineTask) {
    description = "Build baseline for all modules\""
    parallel = true
    ignoreFailures = true
    buildUponDefaultConfig = true
    setSource(projectSource)
    baseline.set(baselineFile)
    config.setFrom(configFile)
    include(kotlinFiles)
    exclude(resourceFiles, buildFiles, appFiles)
}

apply from: "${rootDir}/scripts/publish-root.gradle"
